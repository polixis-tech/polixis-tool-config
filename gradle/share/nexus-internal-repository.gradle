/**
 * Configures the internal Nexus repository as a artifact repository.
 */

def nexusContextUrl = configProperty("NEXUS_CONTEXT_URL", 'nexusContextUrl')

repositories {
    mavenCentral()

    exclusiveContent {
        forRepository {
            maven {
                credentials {
                    username configProperty("NEXUS_USERNAME", "nexusUsername")
                    password configProperty("NEXUS_PASSWORD", "nexusPassword")
                }
                url = "$nexusContextUrl/maven-internal"
            }
        }

        filter {
            includeGroupByRegex "com\\.(polixis|ardis).*"
        }
    }
}

String configProperty(String envVar, String sysProp) {
    System.getenv(envVar) ?: project.findProperty(sysProp)
}

String configProperty(String envVar) {
    configProperty(envVar, envVar.replace('_', '.').toLowerCase())
}
